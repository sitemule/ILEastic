**FREE

/if defined (JWT)
/eof
/endif

/define JWT

///
// ILEastic : JWT Service Program
//
// This service program offers procedures for signing and verifying JWT tokens.
//
// The padding character = is stripped from the end of the token.
//
// @author Mihael Schmidt
// @date   04.05.2019
// @project ILEastic
///

///
// The available signing algorithms
///
dcl-c JWT_HS256 'HS256';
dcl-c JWT_HS384 'HS384';
dcl-c JWT_HS512 'HS512';
dcl-c JWT_RS256 'RS256';
dcl-c JWT_RS384 'RS384';
dcl-c JWT_RS512 'RS512';
dcl-c JWT_ALG_NONE 'none';

///
// Template for JWT token.
///
dcl-s jwt_token_t varchar(65530) template;

///
// Template for the signing key used to create the token signature
///
dcl-s jwt_signkey_t varchar(2040) template;

///
// Template for registered claims. The data structure needs to be created with
// inz(*likeds). Default values mean that the claim will not be added to the
// token. Note: Fields are varchar, and any space will be added to the token; use
// %trimr if you are using char variables.
///
dcl-ds jwt_claims_t qualified template;
  issuer varchar(1000) inz;
  subject varchar(1000) inz;
  audience varchar(1000) inz;
  expirationTime timestamp inz(*loval);
  notBefore timestamp inz(*loval);
  issuedAt timestamp inz(*loval);
  jwtId varchar(1000) inz;
end-ds;

///
// Templfate for JWT options/configuration for token verification and generation
///
dcl-ds jwt_options_t qualified template;
    kid char(100);
    // We are binding the key to a fixed alg to avoid bad actors using a public key to symmetric
    // encryption to get access. https://auth0.com/blog/critical-vulnerabilities-in-json-web-token-libraries/
    // This is not an implementation requirement, but we are placing this restriction for safety.
    alg char(100);
    key like(jwt_signkey_t) ccsid(*utf8);
    // Call back proc for token validation. The caller will have to implement
    validate pointer(*proc);
end-ds;

///
// Verify token
//
// Verifies the token validity. It also checks if it is expired if the token
// contains an "exp" claim. The "exp" claim is expected to be a number
// representing the seconds from the Unix Epoch UTC.
//
// @param Token
// @param Options for verification
// @return <code>*on</code> = valid and not expired else <code>*off</code>
///
dcl-pr jwt_verify ind extproc(*dclcase);
  token like(jwt_token_t) const ccsid(*utf8);
  options likeDs(jwt_options_t) const;
end-pr;

///
// Decode header
//
// Returns the decoded header from the passed token.
//
// @param Token
// @return Decoded header
///
dcl-pr jwt_decodeHeader like(jwt_token_t) ccsid(*utf8) extproc(*dclcase);
  token like(jwt_token_t) const ccsid(*utf8);
end-pr;

///
// Decode payload
//
// Returns the decoded payload from the passed token.
//
// @param Token
// @return Decoded payload
///
dcl-pr jwt_decodePayload like(jwt_token_t) ccsid(*utf8) extproc(*dclcase);
  token like(jwt_token_t) const ccsid(*utf8);
end-pr;

///
// Create JWT token
//
// Creates a JWT token by creating a signature from the header + payload with
// the passed signing key. If the claims data structure is passed then 
// every non-default value will be added to the token payload.
//
// @param Algorithm (f. e. JWT_HS256)
// @param JWT header
// @param JWT payload
// @param Signing key (it has to be of a valid length corresponding to the
//        selected algorithm (HS256: key >= char(32))
// @param Registered claims
// @return Signed JWT token
///
dcl-pr jwt_sign like(jwt_token_t) ccsid(*utf8) extproc(*dclcase);
  algorithm char(100) const;
  header like(jwt_token_t) ccsid(*utf8) const;
  payload like(jwt_token_t) ccsid(*utf8) const;
  options likeds(jwt_options_t) const;
  claims likeds(jwt_claims_t) const options(*nopass);
end-pr;

///
// Check token expiration
//
// Checks if the token is expired. If the token contains no "exp" claim the
// token has not expired.
//
// @param JWT token or payload
// @return <code>*on</code> = token has expired else <code>*off</code>
///
dcl-pr jwt_isExpired ind extproc(*dclcase);
  payloadOrToken like(jwt_token_t) const ccsid(*utf8);
end-pr;
